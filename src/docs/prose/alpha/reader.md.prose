◊(require
   '[docs.alpha.tags :as t]
   '[instaparse.core :as insta]
   '[instaparse.combinators :as instac]
   '[fr.jeremyschoffen.prose.alpha.reader.grammar.utils :as gu]
   '[clojure.pprint :as pp])


# Prose's reader implementation.

The goal of prose's reader is to translate prose style text into clojure data
that can be evaluated by clojure's `eval` function. To do so we make use of the
instaparse and edamame libraries. Instaparse helps us separate text that is text
from text that is code. Edamame is used to read textual code into clojure data.

We explore here the way our reader is constructed.

## The instaparse grammar
We use instaparse to express prose's grammar and generate it's parser.
Instaparse doesn't make a distinction between lexer and parser. Still it is
usefull to separate our grammar using this usual lexer / grammar dichotomy in
the code.

### The lexer.
Our lexer is made of regular expressions constructed with the
◊t/sym[gu/def-regex] macro. It uses the Regal library under the covers.

To assemble these regexes into a lexer we use the ◊t/sym[gu/make-lexer] macro.

Using instaparse and some helpers
◊t/clj {
(require '[instaparse.core :as insta])
(require '[instaparse.combinators :as instac])
(require '[fr.jeremyschoffen.prose.alpha.reader.grammar.utils :as gu])
(require '[clojure.pprint :as pp])
}

◊t/code-s {
(defn p [s] (-> s pp/pprint with-out-str))
}

we can construct the following 2 rules lexer:
◊t/code {
(gu/def-regex number [:* :digit])

(gu/def-regex word [:* [:class ["a" "z"]]])

(def lexer (gu/make-lexer number word))

(p lexer))
}

Note that our lexer is actually a (partial) instaparse grammar.


### The grammatical rules
Most of the grammatical rules are created using the ebnf notation:
◊t/code {
(def rules
  (instac/ebnf
    "
    doc = (token <':'>)*
    token = (number | word)
    "))

(p rules))
}


### Combining the two
Now that we have both a lexer and a grammar, we can simply merge them to make
our parser.
◊t/code {
(def parser
  (insta/parser (merge lexer rules)
                :start :doc))

(p (parser "abc:1:def:2:3:")))
}


## The reader
Prose's parser generated with instaparse is insufficient to constitute a reader
by itself.

We can compare the results of the parser and the reader on an example.

◊t/code-s {
(require '[fr.jeremyschoffen.prose.alpha.reader.core :as reader])
(require '[fr.jeremyschoffen.prose.alpha.reader.grammar :as g])
(require '[fr.jeremyschoffen.prose.alpha.document.lib :as lib])
}

◊t/code {
(def example (lib/slurp-doc "prose/alpha/reader/example.prose"))

example
}

The parser by itself distinguishes between text and code:

◊t/code {
(-> example g/parser p)
}

The ◊t/sym[reader/clojurize] function takes the result of the parser and
re-arranges it into evaluable data:
◊t/code {
(-> example g/parser reader/clojurize p)
}

This reading in 2 phases is provided by the ◊t/sym[reader/read-from-string]
function.
